buildscript {
    ext {
        internalLibraries = [
                project(':public'),
                project(':server-public')
        ]
    }
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

dependencies {
    internalLibraries.each { api it }

    implementation 'org.springframework:spring-web'
}

jar {
    from {
        internalLibraries.collect { it.sourceSets.main.output }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    from {
        internalLibraries.collect { it.sourceSets.main.allJava }
    }
    archiveClassifier = 'sources'
}

javadoc {
    source internalLibraries.collect { it.sourceSets.main.allJava }
    classpath += files(internalLibraries.collect { it.sourceSets.main.compileClasspath })
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'Server Client Library'
                withXml {
                    Node pomNode = asNode()
                    pomNode.dependencies.'*'.each {
                        Node temp = it
                        internalLibraries.each {
                            if (temp.groupId.text() == it.getGroup() && temp.artifactId.text() == it.getName()) {
                                temp.parent().remove(temp)
                                return false
                            }
                        }
                    }
                }
            }
        }
    }
}

//signing {
//    sign publishing.publications.mavenJava
//}
